; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{24A39098-A780-4665-8946-3E7DEB730502}
AppName=Siebel Repository Analyser
AppVersion=1.18
AppPublisher=Ollerenshaw IT Ltd
AppPublisherURL=http://www.siebel-tech.com/
AppSupportURL=http://www.siebel-tech.com/
AppUpdatesURL=http://www.siebel-tech.com/
DefaultDirName={pf}\Siebel Repository Analyser
DefaultGroupName=Siebel Repository Analyser
AllowNoIcons=yes
LicenseFile=InstallerLicense.txt
OutputDir=.
OutputBaseFilename=SiebelReposAnalyser\NewInstaller\rep_analyser_setup
Compression=lzma
SolidCompression=yes
ShowLanguageDialog=auto
SourceDir=..\..
UninstallDisplayName=Siebel Repository Analyser
UninstallDisplayIcon={app}\SiebelReposAnalyser.exe
UsePreviousTasks=False

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
Name: "catalan"; MessagesFile: "compiler:Languages\Catalan.isl"
Name: "corsican"; MessagesFile: "compiler:Languages\Corsican.isl"
Name: "czech"; MessagesFile: "compiler:Languages\Czech.isl"
Name: "danish"; MessagesFile: "compiler:Languages\Danish.isl"
Name: "dutch"; MessagesFile: "compiler:Languages\Dutch.isl"
Name: "finnish"; MessagesFile: "compiler:Languages\Finnish.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "greek"; MessagesFile: "compiler:Languages\Greek.isl"
Name: "hebrew"; MessagesFile: "compiler:Languages\Hebrew.isl"
Name: "hungarian"; MessagesFile: "compiler:Languages\Hungarian.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "norwegian"; MessagesFile: "compiler:Languages\Norwegian.isl"
Name: "polish"; MessagesFile: "compiler:Languages\Polish.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "serbiancyrillic"; MessagesFile: "compiler:Languages\SerbianCyrillic.isl"
Name: "serbianlatin"; MessagesFile: "compiler:Languages\SerbianLatin.isl"
Name: "slovenian"; MessagesFile: "compiler:Languages\Slovenian.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "ukrainian"; MessagesFile: "compiler:Languages\Ukrainian.isl"

#include ReadReg(HKEY_LOCAL_MACHINE,'Software\Sherlock Software\InnoTools\Downloader','ScriptPath','');

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "installdotnet"; Description: "Download and install .NET 4.5 Framework"; GroupDescription: "Additional Software"

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "SiebelReposAnalyser\SiebelReposAnalyser\bin\Release\SiebelReposAnalyser.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\SiebelRepository\bin\Release\SiebelRepository.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\SiebelReposAnalyser\bin\Release\CustomControls.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\SiebelReposAnalyser\bin\Release\Microsoft.WindowsAPICodePack.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\wyBuild\wyUpdate\wyUpdate.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\wyBuild\wyUpdate\client.wyc"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\SiebelReposAnalyser\bin\Release\AutomaticUpdater.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\SiebelReposAnalyser\bin\Release\ObjectListView.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\SiebelReposAnalyser\bin\Release\System.Windows.Forms.Ribbon35.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\SiebelReposAnalyser\bin\Release\ListViewPrinter.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "SiebelReposAnalyser\SiebelReposAnalyser\bin\Release\ScintillaNET.dll"; DestDir: "{app}"; Flags: ignoreversion

; Splash Screen
Source: "InstallerSplash.png"; DestDir: {tmp}; Flags: ignoreversion dontcopy nocompression
Source: "isgsg.dll"; DestDir: {tmp}; Flags: ignoreversion dontcopy nocompression

[Icons]
Name: "{group}\Siebel Repository Analyser"; Filename: "{app}\SiebelReposAnalyser.exe"
Name: "{group}\{cm:UninstallProgram,Siebel Repository Analyser}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\Siebel Repository Analyser"; Filename: "{app}\SiebelReposAnalyser.exe"; Tasks: desktopicon

[Run]
Filename: "{tmp}\dotNetFx45_Full_x86_x64.exe"; Tasks: installdotnet
Filename: "{app}\SiebelReposAnalyser.exe"; Flags: nowait postinstall skipifsilent; Description: "{cm:LaunchProgram,Siebel Repository Analyser}"

[Code]
procedure ShowSplashScreen(p1:HWND;p2:string;p3,p4,p5,p6,p7:integer;p8:boolean;p9:Cardinal;p10:integer); external 'ShowSplashScreen@files:isgsg.dll stdcall delayload';

function DotNetIsInstalled():Boolean;
var
  regVersion:cardinal;
begin
  // Determine if dotNET 4.5 is installed on the local machine
  RegQueryDWordValue(HKLM, 'Software\Microsoft\NET Framework Setup\NDP\v4\Full', 'Release', regVersion);
  Result := regVersion >= 378389;
end;

procedure CurPageChanged(CurPageID: Integer);
var
  status: boolean;
  resultCode: integer;
begin
  if CurPageID = wpSelectTasks then
  begin
    if DotNetIsInstalled then
    begin
      // On entering the Select Tasks page, if dotNET is already installed, uncheck the task
      WizardForm.TasksList.Checked[3] := False;
    end;
  end;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageId = wpSelectTasks Then
  begin
    if IsTaskSelected('installdotnet') Then
    begin
      ITD_AddFile('http://go.microsoft.com/fwlink/?LinkId=225702',expandconstant('{tmp}\dotNetFx45_Full_x86_x64.exe'));
      ITD_DownloadAfter(wpReady);
    end;
  end;
  Result:= True;
end;

procedure InitializeWizard();
begin
  ExtractTemporaryFile('InstallerSplash.png');
  ShowSplashScreen(WizardForm.Handle,ExpandConstant('{tmp}\InstallerSplash.png'),1000,3000,1000,0,255,True,$FFFFFF,10);
  ITD_Init;
  ITD_SetOption('UI_AllowContinue', '1');
end;